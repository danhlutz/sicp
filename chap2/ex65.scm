(define (entry tree) (car tree))

(define (left-branch tree) (cadr tree))

(define (right-branch tree) (caddr tree))

(define (make-tree entry left right)
  (list entry left right))

(define (element-of-set? x set)
  (cond ((null? set) false)
        ((= x (entry set)) true)
        ((< x (entry set))
         (element-of-set? x (left-branch set)))
        ((> x (entry set))
         (element-of-set? x (right-branch set)))))

(define (adjoin-set x set)
  (cond ((null? set) (make-tree x '() '()))
        ((= x (entry set)) set)
        ((< x (entry set))
         (make-tree (entry set)
                    (adjoin-set x (left-branch set))
                    (right-branch set)))
        ((> x (entry set))
         (make-tree (entry set)
                    (left-branch set)
                    (adjoin-set x (right-branch set))))))

(define (tree->list tree)
  (define (copy-to-list tree result-list)
    (if (null? tree)
        result-list
        (copy-to-list (left-branch tree)
                      (cons (entry tree)
                            (copy-to-list (right-branch tree)
                                          result-list)))))
  (copy-to-list tree '()))

(define (list->tree elements)
  (car (partial-tree elements (length elements))))

(define (partial-tree elts n)
  (if (= n 0)
      (cons '() elts)
      (let ((left-size (quotient (- n 1) 2)))
        (let ((left-result (partial-tree elts left-size)))
          (let ((left-tree (car left-result))
                (non-left-elts (cdr left-result))
                (right-size (- n (+ left-size 1))))
            (let ((this-entry (car non-left-elts))
                  (right-result (partial-tree (cdr non-left-elts)
                                              right-size)))
              (let ((right-tree (car right-result))
                    (remaining-elts (cdr right-result)))
                (cons (make-tree this-entry left-tree right-tree)
                      remaining-elts))))))))

(define (union-set s1 s2)
  (define (iter set1 set2)
    (cond ((null? set1) set2)
          ((null? set2) set1)
          ((= (car set1) (car set2))
           (cons (car set1) (iter (cdr set1) (cdr set2))))
          ((< (car set1) (car set2))
           (cons (car set1) (iter (cdr set1) set2)))
          ((> (car set1) (car set2))
           (cons (car set2) (iter set1 (cdr set2))))))
  (let ((set1-list (tree->list s1))
        (set2-list (tree->list s2)))
    (let ((result-list (iter set1-list set2-list)))
      (list->tree result-list))))

(define (intersection-set s1 s2)
  (define (iter set1 set2)
    (cond ((or (null? set1) (null? set2)) '())
          ((= (car set1) (car set2))
           (cons (car set1) (iter (cdr set1) (cdr set2))))
          ((< (car set1) (car set2))
           (iter (cdr set1) set2))
          ((> (car set1) (car set2))
           (iter set1 (cdr set2)))))
  (let ((set1-list (tree->list s1))
        (set2-list (tree->list s2)))
    (let ((result-list (iter set1-list set2-list)))
      (list->tree result-list))))
